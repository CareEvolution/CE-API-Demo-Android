apply plugin: 'com.android.application'

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'com.careevolution.apidemo'
        minSdkVersion 24
        targetSdkVersion 28
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        versionCode 1
        versionName '1.0'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Adagger.gradle.incremental']
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

ext {
    android_support_version='28.0.0'
    android_arch_version = '1.1.1'
    dagger_version = '2.18'
}
dependencies {
    // Google Libraries
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Dependency Injection
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation 'com.jakewharton:butterknife-reflect:9.0.0-rc1'

    // Architecture Components
    implementation "android.arch.lifecycle:common-java8:$android_arch_version"
    implementation "android.arch.lifecycle:extensions:$android_arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$android_arch_version"

    // OAuth 2.0
    implementation('net.openid:appauth:0.7.1') {
        exclude group:'com.android.support', module:'customtabs'
    }
    runtimeOnly "com.android.support:customtabs:$android_support_version"

    // FHIR
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-android:3.5.0'
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:3.5.0'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}
